plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    namespace 'com.sxiaozhi.fragment'
    compileSdk 34

    defaultConfig {
        applicationId "com.sxiaozhi.fragment"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "arm64-v8a", "x86_64", 'x86'
            // 原来是只支持32 下面的这些都可以加"arm64-v8a” 'x86', 'x86_64', 'mips', 'mips64'
        }
    }

    signingConfigs {
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled project.hasProperty('coverage')
            debuggable true
        }

        release {
            lintOptions {
                checkReleaseBuilds false
                abortOnError false
            }
            minifyEnabled false
            shrinkResources false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }

    //自定义打包时apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->// each 改为 all
            def date = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08"))
            def fileName = "${date}_Nav_${productFlavors[0].name}_${versionName}.apk"
            def outFile = output.outputFile
            if (outFile != null && outFile.name.endsWith('.apk')) {
                outputFileName = fileName // output.outputFile 改为 outputFileName
            }
        }
    }

    //多渠道打包
    flavorDimensions "channel"
    productFlavors {
        configureEach {
            buildConfigField "String", "API_URL", "\"http://wifi.3xiaozhi.com/\""
        }
        common {
            dimension 'channel'
            resValue "string", "APP_NAME", findProperty("productNameForCommon") as String
        }
        somewhat {
            dimension 'channel'
            resValue "string", "APP_NAME", findProperty("productNameForSomewhat") as String
        }
        annoying {
            dimension 'channel'
            resValue "string", "APP_NAME", findProperty("productNameForAnnoying") as String
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "androidx.recyclerview:recyclerview:1.3.1"

    implementation "com.google.dagger:hilt-android:2.46.1"
    kapt "com.google.dagger:hilt-compiler:2.46.1"

    // retrofit2
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.13.2"
    kapt "com.github.bumptech.glide:compiler:4.13.2"
    implementation "jp.wasabeef:glide-transformations:4.3.0"
}